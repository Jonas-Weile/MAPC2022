use "../../mapcActions" as actionspec.
use "../../knowledge_generated" as knowledge.

module explore {

	% Find some nice, pretty blocks
	if goal(fullyEquipped), bel(myRole(MyRole)), bel(canAttach(MyRole)) then {
		
		% We have found a block/dispenser
		if bel(closestBlockOrDispenserInVision(Xr, Yr, _, _)) then {
			if bel(adjacent(0, 0, Xr, Yr), rotationRequiredToAttach(Xr, Yr)) then {
			
				if bel(possibleRotation(Xr, Yr, R)) then rotate(R).
				
				if bel(validDirection(D)) then move(D).
			}			
			
			if bel(thing(Xr, Yr, block, _), not(attached(Xr, Yr)), direction(Xr, Yr, Dir))
				then attach(Dir).
				
			if bel(thing(Xr, Yr, dispenser, _), direction(Xr, Yr, Dir))
				then request(Dir).
		
			% We are not next to block nor dispenser, but we have our eyes on the prize
			if bel(goToAction(Xr, Yr, Action)) 
			then {
				if bel(Action = move(D)) then move(D).
				if bel(Action = rotate(R)) then rotate(R).
				if bel(Action = clear(Xc, Yc)) then clear(Xc, Yc).
			}
		}
	}
	
	% If I see a role zone, change to a new role better suited for exploring!
	if bel(myRole(MyRole)), not(bel(canAttach(MyRole))), bel(roleZone(Xr, Yr)) then {
	
		% Change role if we are in a roleZone
		%% OBS. For now, we hard-code the worker role
		if bel(roleZone(0, 0)) then adapt(worker).
	
		% Go towards the roleZone
		if bel(goToAction(Xr, Yr, Action)) 
		then {
			if bel(Action = move(D)) then move(D).
			if bel(Action = rotate(R)) then rotate(R).
			if bel(Action = clear(Xc, Yc)) then clear(Xc, Yc).
		}
	}
	
	
	% .. Otherwise, keep on exploring
	if goal(explore) then {
		if bel(exploreAction(Action)) then {
				if bel(Action = move(D)) then move(D).
				if bel(Action = rotate(R)) then rotate(R).
				if bel(Action = clear(Xc, Yc)) then clear(Xc, Yc).
		}
	}
}