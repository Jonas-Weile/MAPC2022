use "../../knowledge_generated" as knowledge.

module sendConnectionRequests {	
	
	% Send connection requests
	if bel(name(MyName), myPosition(MyX, MyY), step(Step)) then {
	
		% Find all adjacent agents
		if bel(findAdjacentAgents(AdjacentAgents)) then {
			
			% For each agent, find all common Environment percepts, send these to all explorers, and save them
			forall bel(member((Xr, Yr), AdjacentAgents)), 
				   bel(identifyCommonEnvironmentPercepts(Xr, Yr, CEP)), 
				   bel(CEP \= []) 	
				   	
			do insert(savedCommonEnvironmentPercepts((MyX, MyY), (Xr, Yr, CEP), Step)) +
			   insert(commonEnvironmentPercepts(Xr, Yr, CEP)).	     
		}   
	
		% Send all found CEP's
		listall CEPs <- bel(commonEnvironmentPercepts(Xr, Yr, CEP))
			 do (explore).send?(connectionRequest(MyName, MyX, MyY, CEPs, Step)).	
		
		% Send empty update if we didn't find any other agents that we could connect to.
		if not(bel(commonEnvironmentPercepts(_, _, _))) 
			then (explore).send?(connectionRequest(MyName, MyX, MyY, [0, 0, []], Step)). 
	}
	
	% delete common environment percepts
	forall bel(commonEnvironmentPercepts(AnyX, AnyY, AnyCEP)) do
		delete(commonEnvironmentPercepts(AnyX, AnyY, AnyCEP)).
}	





